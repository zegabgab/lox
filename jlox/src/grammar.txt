program        -> declaration* EOF ;
declaration    -> varDeclaration | statement ;
varDeclaration -> "var" IDENTIFIER ( "=" expression)? ";" ;
statement      -> printStmt | exprStmt | block ;
printStmt      -> "print" expression ";" ;
exprStmt       -> expression ";" ;
block          -> "{" declaration* "}" ;

expression     -> assignment ;
assignment     -> IDENTIFIER "=" assignment | equality ;
equality       -> comparison ( ( "==" | "!=" ) comparison )* ;
comparison     -> term ( ( "<" | "<=" | ">" | ">=" ) term )* ;
term           -> factor ( ( "+"  | "-" ) factor )* ;
factor         -> unary ( ( "*" | "/" ) unary )* ;
unary          -> ( "!" | "-" ) unary
                | primary ;
primary        -> "(" expression ")"
                | NUMBER | STRING | IDENTIFIER | "true" | "false" | "nil" ;